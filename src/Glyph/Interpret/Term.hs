{-# LANGUAGE ScopedTypeVariables #-}
module Glyph.Interpret.Term
  ( Term(..) ) where

import Prelude hiding (head, lookup)
import Data.Maybe
import Control.Monad.Except (MonadError, throwError)

import Prettyprinter

import Glyph.Abstract.Syntax
import Glyph.Abstract.Environment
import Glyph.Abstract.AlphaEq
  

{------------------------------ THE TERM CLASSES -------------------------------}
{- The Term class supports only two methods:                                   -}
{- â€¢ normalize: convert to canonical (Î’-normal, Î·-long) form                   -}
{- â€¢ equiv: Î±Î²Î· equivalence                                                    -}
{-                                                                             -}
{- Both accept an environment. Currently, this is a local environment, but     -}
{- eventually the environment will also include a 'global' (i.e. surrounding   -}
{- module) component as well, to look up qualified names (QName)               -}
{-                                                                             -}
{- There is also the TermDec class, which must be fulfilled by any Decorations -}
{- used on the Term Syntax tree.                                               -}
{-                                                                             -}
{-------------------------------------------------------------------------------}


class Pretty a => Term a where
  normalize :: (MonadError (Doc ann) m, Environment Name e) => e (Maybe a,a) -> a -> a -> m a
  equiv :: (MonadError (Doc ann) m, Environment Name e) => e (Maybe a,a) -> a -> a -> a -> m Bool

-- class (Default Ï‡, DecEq Ï‡, DecPretty Ï‡) => TermDec Ï‡ where  
--(Eq (Core AnnBind Name Ï‡), Pretty (Core AnnBind Name Ï‡))
--(Pretty (Core AnnBind Name Ï‡), MonadError (Doc ann) m, Environment Name e)
--(Pretty (Core AnnBind Name Ï‡), MonadError (Doc ann) m, Environment Name e)

{------------------------------ DENOTATIVE TERMS -------------------------------}
{- These are types for a denotative interpretation of expressions in core:     -}
{- Most look similar to their 'normal' semantic representation, with the       -}
{- important exception of functions, which are represented as closures,        -}
{- capturing their environment.                                                -}
{-                                                                             -}
{- There are also types for Neutral and Normal terms, which are terms that     -}
{- have been evaluated as much as is possible. Normal Terms have the form of   -}
{- a Î»-abstraction, Î -type Universe or Constant. Importantly, normal terms are -}
{- accompanied by their type. Neutral terms are those whose evaluation is      -}
{- blocked because of an uninstantiated variable, e.g. f 2, where f is an      -}
{- uninstantiated variable.                                                    -}
{-------------------------------------------------------------------------------}


data Sem e b Ï‡
  = SUni (UniÏ‡ Ï‡) Int
  | SPrd (PrdÏ‡ Ï‡) Name (Sem e b Ï‡) (Sem e b Ï‡)
  | SAbs Name (Core b Name Ï‡) (e (Sem e b Ï‡))
  | Neutral (Sem e b Ï‡) (Neutral e b Ï‡)

data Neutral e b Ï‡
  = NeuVar (VarÏ‡ Ï‡) Name
  | NeuApp (AppÏ‡ Ï‡) (Neutral e b Ï‡) (Normal e b Ï‡)

data Normal e b Ï‡ = Normal (Sem e b Ï‡) (Sem e b Ï‡)


{-------------------------------- TERM INSTANCE --------------------------------}
{- The term instance for Core applies type directed normalization by           -}
{- evaluation. Equality is derived from the Eq instance defined in Core.hs,    -}
{- extended to first perform normalization.                                    -}
{-                                                                             -}
{- Normalization relies on a few key helper functions:                         -}
{- â€¢ eval performs untyped evaluation, converting any term into a Semantic     -}
{-   term. eval has a helper function, app, which performs both function and   -}
{-   type application.                                                         -}
{- â€¢ env_eval evaluates each term in an environment, returning a semantic      -}
{-   environment.                                                              -}
{- â€¢ read_nf takes a term in normal form, and reads it back into a Core term.  -}
{-   this is type-directed because all normal terms have an accompanying type. -}
{- â€¢ read_ne takes a term in netural form, and reads it back into a Core term. -}
{-                                                                             -}
{-------------------------------------------------------------------------------}


-- TODO: now we use IDs for names, need to ensure we do capture-avoiding substitution!!
instance (AlphaEq Name (Core AnnBind Name Ï‡), Pretty (Core AnnBind Name Ï‡), ForallÏ‡ Monoid Ï‡) => Term (Core AnnBind Name Ï‡) where
  normalize env ty term =
    read_nf =<< (Normal <$> ty' <*> term')
    where
      ty' = eval ty =<< env_eval env
      term' = eval term =<< env_eval env

  equiv env ty x y = (Î±eq) <$> normalize env ty x <*> normalize env ty y


read_nf :: forall e Ï‡ m b ann. (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
  Normal e b Ï‡ -> m (Core b Name Ï‡)
read_nf (Normal ty val) = case (ty, val) of 
  (SPrd _ name a b, f) -> do
    let neua :: Sem e b Ï‡ 
        neua = Neutral a $ NeuVar mempty name
    
        lvl = uni_level a
    a' <- read_nf $ Normal (SUni mempty lvl) a
    f' <- read_nf =<< (Normal <$> (b `app'` neua) <*> (f `app'` neua))
    -- TODO: we can probably derive the Ï‡-decoration from f somehow...
    pure $ Abs mempty (bind name a') f'
  (SUni _ _, SUni Ï‡ i) -> pure $ Uni Ï‡ i
  (SUni Ï‡â‚ k, SPrd Ï‡â‚‚ name a b) -> do
    a' <- (read_nf $ Normal (SUni Ï‡â‚ k) a)
    b' <- (read_nf $ Normal (SPrd Ï‡â‚‚ name a (SUni mempty k)) b)
    pure $ Prd Ï‡â‚‚ (bind name a') b'
        
  (_, Neutral _ e) -> read_ne e 
  (_, _) -> throwError ("bad read_nf: " <+> pretty val <> " : " <+> pretty ty)

read_ne :: (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
             Neutral e b Ï‡ -> m (Core b Name Ï‡)
read_ne neu = case neu of 
  NeuVar Ï‡ name -> pure $ Var Ï‡ name
  NeuApp Ï‡ l r -> App Ï‡ <$> (read_ne l) <*> (read_nf r)

eval :: (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
          Core b Name Ï‡ -> e (Sem e b Ï‡) -> m (Sem e b Ï‡)
eval term env = case term of
  CoreÏ‡ _ -> throwError "cannot eval CoreÏ‡ terms" 
  Uni Ï‡ n -> pure $ SUni Ï‡ n
  Prd Ï‡ bnd b -> do
    nm <- fromMaybe (throwError "Prd must bind a name") (fmap pure $ name bnd)
    a <- fromMaybe (throwError "Prd must bind a type") (fmap pure $ tipe bnd)
    a' <- eval a env
    pure $ SPrd Ï‡ nm a' $ SAbs nm b env
  Var _ name -> lookup_err name env
  Abs _ bnd body -> do
    nme <- fromMaybe (throwError "Abs must bind a name") (fmap pure $ name bnd)
    pure $ SAbs nme body env
  App Ï‡ l r -> do
    l' <- (eval l env)
    r' <- (eval r env)
    app Ï‡ l' r'

app' :: (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
          Sem e b Ï‡ -> Sem e b Ï‡ -> m (Sem e b Ï‡)
app' = app mempty

app :: (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
         AppÏ‡ Ï‡ -> Sem e b Ï‡ -> Sem e b Ï‡ -> m (Sem e b Ï‡)
app _ (SAbs name body env) val = eval body (insert name val env)
app Ï‡ (Neutral (SPrd _ _ a b) neu) v =
  Neutral <$> (b `app'` v) <*> pure (NeuApp Ï‡ neu (Normal a v))
app _ _ _ = throwError "bad args to app"

env_eval :: (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
              e (Maybe (Core b Name Ï‡), Core b Name Ï‡) -> m (e (Sem e b Ï‡))
env_eval = eval_helper eval_var 
  where

    
    eval_var :: (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
                  Name -> (Maybe (Core b Name Ï‡), (Core b Name Ï‡)) -> e (Sem e b Ï‡) -> m (Sem e b Ï‡)
    eval_var n (Nothing, ty) env = mkvar n ty env
    eval_var _ (Just val, _) env = eval val env
    
    mkvar :: (Binding b, Pretty (Core b Name Ï‡), ForallÏ‡ Monoid Ï‡, MonadError (Doc ann) m, Environment Name e) =>
                  Name -> (Core b Name Ï‡) -> e (Sem e b Ï‡) -> m (Sem e b Ï‡)
    mkvar n ty env = do
      ty' <- eval ty env
      pure $ Neutral ty' (NeuVar mempty n)

-- TODO: fix this function - it is wrong!
uni_level :: (Sem e b Ï‡) -> Int
uni_level sem = case sem of 
  SUni _ n -> n + 1
  SPrd _ _ l r -> max (uni_level l) (uni_level r)
  SAbs _ _ _ -> 0 -- note: predicative vs impredicative!!
  Neutral _ _ -> 0 -- TODO: this is probably wrong!!!


{-------------------------------- MISC INSTANCES -------------------------------}
{-                                                                             -}
{-                                                                             -}
{-                                                                             -}
{-------------------------------------------------------------------------------}


instance Pretty (Core b Name Ï‡) => Pretty (Sem e b Ï‡) where
  pretty sem = case sem of 
    SUni _ n -> "ğ’°" <> pretty n
    SPrd _ n a b -> pretty n <> " : " <> pretty a <+> "â†’" <+> pretty b
    SAbs n body _ -> "Î» (" <> pretty n <> ")" <+> pretty body
    Neutral _ n -> pretty n
  
instance Pretty (Core b Name Ï‡) => Pretty (Neutral e b Ï‡) where
  pretty neu = case neu of
    NeuVar _ n -> pretty n
    NeuApp _ l r -> pretty l <+> pretty r

instance Pretty (Core b Name Ï‡) => Pretty (Normal e b Ï‡) where
  pretty (Normal _ val) = pretty val

